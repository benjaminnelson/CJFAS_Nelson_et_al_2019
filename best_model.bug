
model
	{
		# Loop over individual stocks
		for (i in 1:nstocks)
			{
				# Parameters in hierarchical form
				q_seal[i]~dnorm(mean_q, tau_q)
				h_hatch[i]~dnorm(mean_h, tau_h)
								
				# Priors on ricker b (non-hierarchical)
				a_ricker[i]~dnorm(0,1.0E-6)
				b_ricker[i]~dnorm(0,1.0E-6)
				
				rho[i]~dunif(-1,1)						
				tau[i]~dgamma(0.01,0.01)
				
			}

		# Hyper-priors
		
		mean_q~dnorm(0,1.0E-6)
		mean_h~dnorm(0,1.0E-6)

		# First Brood Year
		# Normal likelihood function
				lnrs_obs[1]~dnorm(lnrs[1], tau[stock_id[1]])
				
				# Linearized Ricker model
				lnrs[1]<- mu[1]
				mu[1]<- a_ricker[stock_id[1]]+(b_ricker[stock_id[1]]*spawners[1])+(q_seal[stock_id[1]]*seals[1])+(h_hatch[stock_id[1]]*hatch[1])
						
				# Posterior predictive dists
				#ppred_lnrs[1]~dnorm(lnrs[1], tau[stock_id[1]])
				
		# Loop over all observations

		for(i in 2:nobs)
			{
				# Normal likelihood function
				lnrs_obs[i]~dnorm(lnrs[i], tau[stock_id[i]])
				
				# Linearized Ricker model
				lnrs[i]<- mu[i]+rho[stock_id[i]]*(lnrs[i-1]-lnrs_obs[i-1])*start_id[i]
				mu[i]<- a_ricker[stock_id[i]]+(b_ricker[stock_id[i]]*spawners[i])+(q_seal[stock_id[i]]*seals[i])+(h_hatch[stock_id[i]]*hatch[i])
				
				# Posterior predictive dists
				#ppred_lnrs[i]~dnorm(lnrs[i], tau[stock_id[i]])

			}

		# Priors for precision/variance parameters
		
		tau_q~dgamma(0.01,0.01)
		sd_q<- 1/sqrt(tau_q)
		tau_h~dgamma(0.01,0.01)
		sd_h<- 1/sqrt(tau_h)		

	}